pipeline GoodsTransportPipeline {

    block GoodsTransportExtractor oftype HttpExtractor {
        url: "https://www-genesis.destatis.de/genesis/downloads/00/tables/46131-0014_00.csv";
    }

    block GoodsTransportTextFileInterpreter oftype TextFileInterpreter {
        encoding: "latin3";
    }

    block GoodsTransportTextRangeSelector oftype TextRangeSelector {
        lineFrom: 8;
        lineTo: 41409;  
    }

    block GoodsTransportCSVInterpreter oftype CSVInterpreter {
        delimiter: ";";
    }

    block GoodsTransportHeaderWriter_AtoE oftype CellWriter {
        at: range A1:E1;
        write: ["year", "month", "goods_id", "goods_name", "goods_source"];
    }

    block GoodsTransportHeaderWriter_ATtoAU oftype CellWriter {
        at: range AT1:AU1;
        write: ["abroad", "total"];
    }

    block GoodsTransportTableInterpreter oftype TableInterpreter {
        header: true;
        columns: [
            "year" oftype PositiveInteger,
            "month" oftype GermanMonth,
            "goods_id" oftype GoodsId,
            "goods_name" oftype text,
            "goods_source" oftype text,
            "abroad" oftype PositiveInteger,
            "total" oftype PositiveInteger
        ];
    }

    block GoodsTransportLoader oftype SQLiteLoader {
        table: "goods";
        file: "./goodsTransportedByTrain.sqlite";
    }

   GoodsTransportExtractor
        -> GoodsTransportTextFileInterpreter
        -> GoodsTransportTextRangeSelector
        -> GoodsTransportCSVInterpreter
        -> GoodsTransportHeaderWriter_AtoE
        -> GoodsTransportHeaderWriter_ATtoAU
        -> GoodsTransportTableInterpreter
        -> GoodsTransportLoader;
}

constraint PositiveRange on decimal:
        value >= 0;

valuetype PositiveInteger oftype integer {
    constraints: [PositiveRange];
}

constraint MonthConstraint oftype AllowlistConstraint {
    allowlist: ["Januar", "Februar", "MÃ¤rz", "April", "Mai", "Juni", "Juli", "August", "September", "Oktober", "November", "Dezember"];
}

valuetype GermanMonth oftype text {
    constraints: [MonthConstraint];
}

constraint GoodsIdConstraint oftype RegexConstraint {
    regex: /^NST7-[A-Z0-9]{3}$/;
}

valuetype GoodsId oftype text {
    constraints: [GoodsIdConstraint];
}
