pipeline MoWeStaTemperaturePipeline {

    block MoWeStaExtractor oftype HttpExtractor {
        url: "https://www.mowesta.com/data/measure/mowesta-dataset-20221107.zip";
    }

    block MoWeStaUnzipper oftype ArchiveInterpreter {
        archiveType: "zip";
    }

    block MoWeStaFilePicker oftype FilePicker {
        path: "/data.csv";
    }

    block MoWeStaTextFileInterpreter oftype TextFileInterpreter {
        encoding: "latin3";
    }

    block MoWeStaCSVInterpreter oftype CSVInterpreter {
        delimiter: ";";
    }

    block MoWeStaColumnRenamer oftype CellWriter {
        at: range A1:E1;
        write: ["id", "producer", "model", "month", "temperature"];
    }

    block MoWeStaBatteryColumnRenamer oftype CellWriter {
        at: cell J1;
        write: ["battery_temperature"];
    }

    block MoWeStaColumnFilter oftype ColumnDeleter {
        delete: [column F, column G, column H, column I];
    }

    block MoWeStaTableInterpreter oftype TableInterpreter {
        header: true;
        columns: [
            "id" oftype PositiveInteger,
            "producer" oftype text,
            "model" oftype text,
            "month" oftype PositiveInteger, 
            "temperature" oftype decimal,
            "battery_temperature" oftype decimal
        ];
    }

    block MoWeStaTemperatureTransformer oftype TableTransformer {
        inputColumns: ["temperature"];
        outputColumn: "temperature";
        use: CelsiusToFahrenheit;
    }

    block MoWeStaBatteryTemperatureTransformer oftype TableTransformer {
        inputColumns: ["battery_temperature"];
        outputColumn: "battery_temperature";
        use: CelsiusToFahrenheit;
    }

    block MoWeStaDataLoader oftype SQLiteLoader {
        table: "temperatures";
        file: "./temperatures.sqlite";
    }
   
    MoWeStaExtractor
        -> MoWeStaUnzipper
        -> MoWeStaFilePicker
        -> MoWeStaTextFileInterpreter
        -> MoWeStaCSVInterpreter
        -> MoWeStaColumnRenamer
        -> MoWeStaBatteryColumnRenamer
        -> MoWeStaColumnFilter
        -> MoWeStaTableInterpreter
        -> MoWeStaTemperatureTransformer
        -> MoWeStaBatteryTemperatureTransformer
        -> MoWeStaDataLoader;
}

    transform CelsiusToFahrenheit {
        from Celsius oftype decimal;
        to Fahrenheit oftype decimal;
        Fahrenheit: (Celsius * 9/5) + 32;
    }

    constraint PositiveRange on decimal:
        value >= 0;

    valuetype PositiveInteger oftype integer {
    constraints: [PositiveRange];
}

  